/*
 * Copyright (c) 2022 xylan.org
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    alias libs.plugins.owasp.dependencycheck
    alias libs.plugins.versions
    alias libs.plugins.spotless
    alias libs.plugins.spotless.changelog
    alias libs.plugins.grgit
}

description = "MailSpy :: Root"

wrapper {
    gradleVersion = libs.versions.gradle.get()
}

allprojects {
    group = "org.xylan.mailspy"
    version = "0.0.0"

    repositories {
        mavenCentral()
    }
}

subprojects {
    if (file("$projectDir/src/main/java").exists()) {
        apply plugin: "checkstyle"
        checkstyle {
            configFile file("$rootDir/config/checkstyle.xml")
            toolVersion = libs.versions.checkstyle.get()
        }

        apply plugin: "pmd"
        pmd {
            ruleSets = []
            ruleSetFiles = files("$rootDir/config/pmd.xml")
            toolVersion = libs.versions.pmd.get()
            consoleOutput = true
        }
    }

    tasks.withType(JavaCompile) {
        options.release = 11
        options.compilerArgs << "-Xlint:-processing" << "-Werror"
    }
}

spotless {
    def exclusions = [
        "**/build/**",
        ".gradle/**",
        ".idea/**",
        ".settings/**",
        ".vscode/**",
        "**/node_modules/**",
        "mailspy-frontend/package-lock.json"
    ]
    format "licenseHeader", {
        target "**/*.gradle", "**/*.java", "**/*.ts", "**/*.tsx", "**/*.js", "**/*.css"
        targetExclude exclusions
        licenseHeaderFile file("$rootDir/config/license/c_like.txt"), "^(?!(\\/\\*| \\*| \\*\\/)).+"
    }
    format "generic", {
        target "**/*.gradle", "**/*.java", "**/*.ts", "**/*.tsx", "**/*.js", "**/*.css", "**/*.json", "**/*.properties", "**/*.yml"
        targetExclude exclusions
        trimTrailingWhitespace()
        endWithNewline()
    }
    groovyGradle {
        target "**/*.gradle"
        targetExclude exclusions
        greclipse().configFile(file("$rootDir/config/greclipse.properties"))
    }
    java {
        target "**/*.java"
        targetExclude exclusions
        palantirJavaFormat(libs.versions.palantirformat.get())
    }
}

spotlessChangelog {
    changelogFile "CHANGELOG.md"
    ifFoundBumpBreaking "**BREAKING**"
    ifFoundBumpAdded "### Added"
    appendDashSnapshotUnless_dashPrelease=true
}

dependencyCheck {
    suppressionFile "$rootDir/config/owasp-suppressions.xml"
    scanConfigurations = ["runtimeClasspath", "compileClasspath"]
    analyzers {
        nodeEnabled = false // cannot skip dev deps, see https://github.com/jeremylong/DependencyCheck/issues/2482#issuecomment-603755623
        assemblyEnabled = false
        nodeAudit {
            skipDevDependencies = true
        }
        retirejs {
            enabled = false
        }
    }
}

dependencyUpdates {
    gradleReleaseChannel = "current"
    revision = "release"
}

def isNonStable = { version ->
    def stableKeyword = ["RELEASE", "FINAL", "GA"].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

def getMajorVersion = { version ->
    def match = version =~ /^(\d+)\..*$/
    return match && match[0] ? match[0][1] as int : null
}

def majorVersionUpdate = { candidateVersion, currentVersion ->
    return getMajorVersion(candidateVersion) > getMajorVersion(currentVersion)
}

tasks.named("dependencyUpdates").configure {
    checkForGradleUpdate = true
    outputFormatter = "html"
    outputDir = "$buildDir/reports"
    reportfileName = "dependency-update-report"
    rejectVersionIf {
        majorVersionUpdate(it.candidate.version, it.currentVersion) || isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

check {
    dependsOn spotlessCheck
}

task updateBuildGradleVersion {
    description = "Updates the version in the build.gradle file. New version is inferred from the changelog."
    group = "versioning"
    doLast {
        def buildFileContents = buildFile.getText()
            .replaceFirst("version = \".*\"", "version = \"" + spotlessChangelog.versionNext + "\"")
        buildFile.setText(buildFileContents)
    }
}

task updateVersion {
    description = "Updates the version in files which contain it. New version is inferred from the changelog."
    group = "versioning"
    dependsOn updateBuildGradleVersion
}

task commitVersion {
    description = "Commits files updated by versioning."
    group = "versioning"
    doLast {
        def version = rootProject.version
        grgit.add(patterns: [
            "build.gradle",
            "mailspy-frontend/package.json",
            "CHANGELOG.md"
        ])
        if (!grgit.status().staged.allChanges.isEmpty()) {
            grgit.commit(message: "Update version to $version")
            grgit.push(all: true)
        }
    }
    mustRunAfter build
}

task tagVersion {
    description = "Creates git tag for the current version."
    group = "versioning"
    doLast {
        def version = rootProject.version
        grgit.tag.add(name: version, message: "Version $version")
        grgit.push(tags: true)
    }
    mustRunAfter build
}

build {
    mustRunAfter updateVersion
}

task doBuild {
    def release = project.findProperty("release")?.toBoolean()
    if (release) {
        dependsOn updateVersion, changelogBump, build, tagVersion
    } else {
        dependsOn updateVersion, build, commitVersion
    }
}
