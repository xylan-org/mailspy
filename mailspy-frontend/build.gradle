plugins {
    id "java-library"
    id "maven-publish"
    alias libs.plugins.siouan.frontend
}

import org.siouan.frontendgradleplugin.infrastructure.gradle.RunNpm

description = "MailSpy :: Frontend"

frontend {
    nodeVersion = "16.15.0"
    assembleScript = "run build"
    installScript = "install --no-audit --no-fund --loglevel=error"
}

assembleFrontend {
    inputs.files("public", "src", "package.json", "tsconfig.json", "craco.config.js")
    outputs.dir("$buildDir/webpack")
    onlyIf {
        project.findProperty("production") ?: false
    }
}

jar {
    archivesBaseName = "mailspy-frontend"
    from (assembleFrontend) {
        into "META-INF/mailspy-frontend"
    }
    dependsOn assembleFrontend
}

task testFrontend(type: RunNpm) {
    description = "Runs the frontend tests."
    group = "verification"
    script = "test -- --watchAll=false"
    dependsOn installFrontend
}

task prettierCheck(type: RunNpm) {
    description = "Runs prettier in check mode."
    group = "verification"
    script = "run prettier-check"
    dependsOn installFrontend
}

task prettierApply(type: RunNpm) {
    description = "Runs prettier in write mode."
    group = "verification"
    script = "run prettier-apply"
    dependsOn installFrontend
}

check {
    dependsOn testFrontend
    dependsOn prettierCheck
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
        }
    }
}
