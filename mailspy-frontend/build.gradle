plugins {
    id "java-library"
    id "maven-publish"
    alias libs.plugins.siouan.frontend
}

import org.siouan.frontendgradleplugin.infrastructure.gradle.RunNpm

description = "MailSpy :: Frontend"

frontend {
    nodeVersion = "12.18.1"
    assembleScript = "run build"
    installScript = "install --no-audit --no-fund --loglevel=error"
}

assembleFrontend {
    inputs.files("public", "src", "package.json", "tsconfig.json", "craco.config.js")
    outputs.dir("$buildDir/webpack")
    onlyIf {
        project.findProperty("production") ?: false
    }
}

task jarFrontend(type: Jar) {
    archivesBaseName = "mailspy-frontend"
    from (assembleFrontend) {
        into "META-INF/mailspy-frontend"
    }
    dependsOn assembleFrontend
}

task testFrontend(type: RunNpm) {
    description = "Runs the frontend tests."
    group = "verification"
    script = "test -- --watchAll=false"
    dependsOn installFrontend
}

check {
    dependsOn testFrontend
}

artifacts {
    add("default", jarFrontend)
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact jarFrontend
        }
    }
}
